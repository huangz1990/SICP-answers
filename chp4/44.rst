练习 4.44
============

该问题的程序如下::
   
  ;; 辅助过程，用于测试是否在对角线上
  (ambeval '(define (diagonal? item list number)
              (if (null? list)
                  false
                  (let ((diff (- item (car list))))
                    (or (= diff (+ 1 (- number (length list))))
                        (= (- diff) (+ 1 (- number (length list))))
                        (diagonal? item (cdr list) number)))))
           the-global-environment
           (lambda (value fail) value)
           (lambda () 'failed))

  ;; 辅助过程，用于测试是否在同一水平线上
  (ambeval '(define (member? item list)
              (if (null? list)
                  false
                  (if (equal? item (car list))
                      true
                      (member? item (cdr list)))))
           the-global-environment
           (lambda (value fail) value)
           (lambda () 'failed))

  (ambeval '(define (eight-queens)
              (define (loop queen-list number)
                (if (= number 8)
                    queen-list
                    (let ((queen (amb 1 2 3 4 5 6 7 8)))
                      (require (not (member? queen queen-list)))
                      (require (not (diagonal? queen queen-list number)))
                      (loop (cons queen queen-list) (+ number 1)))))
              (loop '() 0))
           the-global-environment
           (lambda (value fail) value)
           (lambda () 'failed))

